apply plugin: 'com.android.application'

android {
    namespace 'org.orx.demo'
    compileSdk sdkVersion.toInteger()

    // Ideally, we want to use the default NDK version for the used AGP version.
    // However, we opt-in to NDK r26 since it comes with many important features/optimizations.
    ndkVersion '26.2.11394342'

    buildFeatures.prefab true

    defaultConfig {
        applicationId 'org.orx.demo'
        versionCode 1
        versionName '1.0'

        minSdk 21
        targetSdk sdkVersion.toInteger()

        vectorDrawables.useSupportLibrary = true

        manifestPlaceholders = [ gameArguments: '' ]
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_11
        sourceCompatibility JavaVersion.VERSION_11
    }
    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }
    sourceSets {
        main {
            // For custom apps, you should have resources in some parent folder with orx data.
            // E.g. '../../../data'
            assets.srcDirs = ['./src/main/assets']
        }
    }
    buildTypes {
        release {
            debuggable false

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            externalNativeBuild.ndkBuild {
                arguments 'NDK_DEBUG=0', '-j'
            }
            manifestPlaceholders = [ gameArguments: 'orxDemo' ]
        }
        debug {
            debuggable true

            externalNativeBuild.ndkBuild {
                arguments 'NDK_DEBUG=1', '-j'
                cFlags '-D__orxDEBUG__'
            }
            manifestPlaceholders = [ gameArguments: 'orxDemoDebug' ]
        }
        profile {
            debuggable true

            externalNativeBuild.ndkBuild {
                arguments 'NDK_DEBUG=0', '-j'
                cFlags '-D__orxPROFILER__'
            }
            manifestPlaceholders = [ gameArguments: 'orxDemoProfile' ]
        }
    }
    lint {
        abortOnError true
    }
    androidResources {
        noCompress = [ '.obr', '.ktx2' ]
    }
}
dependencies {
    releaseImplementation 'org.orx-project:orx:1.15'
    debugImplementation 'org.orx-project:orxd:1.15'
    profileImplementation 'org.orx-project:orxp:1.15'
}
