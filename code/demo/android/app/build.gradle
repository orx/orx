apply plugin: 'com.android.application'

def orx = System.getenv('ORX') ?: "../../.."

android {
  compileSdkVersion sdkVersion.toInteger()

  // NDK: https://developer.android.com/studio/projects/install-ndk#default-ndk-per-agp
  // Verify result of ndk_locator in android/app/.cxx/ndk_locator_record_*.log
  ndkVersion "21.3.6528147"

  defaultConfig {
    applicationId "org.orxproject.orxtest"
    targetSdkVersion sdkVersion.toInteger()
    versionCode 1
    versionName "1.0"

    minSdkVersion 17

    ndk {
      abiFilters "x86", "armeabi-v7a", "arm64-v8a", "x86_64"
    }
  }

  lintOptions {
    abortOnError true
    warningsAsErrors true
  }

  buildTypes {
    release {
      debuggable false
      shrinkResources true
      minifyEnabled true

      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
    debug {
      debuggable true
      shrinkResources false
      minifyEnabled false
    }
  }

  aaptOptions {
    ignoreAssetsPattern "!CreationTemplate.ini:!SettingsTemplate.ini"
  }

  externalNativeBuild {
    ndkBuild {
      path 'src/main/jni/Android.mk'
    }
  }

  sourceSets {
    main {
      java.srcDirs += ["${orx}/build/android/java"]

      // For custom apps, you should have resources in some parent folder with orx data.
      // E.g. '../../../data'
      assets.srcDirs = ['./src/main/assets']
    }
  }
}

repositories {
  maven { url 'https://maven.google.com' }
}

dependencies {
  implementation 'androidx.fragment:fragment:1.3.1'

  implementation fileTree(include: ['*.jar'], dir: 'libs')
}
